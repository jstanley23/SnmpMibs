* from Jim Dutton, CA0008@SIUCVMB.SIU.EDU
*
* Please feel free to do whatever with it you wish, but it is supplied "as is".
*
SNMPALRT CLIST
&CONTROL ERR
*
&MAXRETRY = 3
&LINECOUNT = 1
&MIBVAR = 0
&RETCODE = 0

&CISASNID  = '1.3.6.1.4.1.9.'
&IBMASNID  = '1.3.6.1.4.1.2.'
&INETASNID = 0

&AGSINTCLIEN = '1.3.6.1.4.1.9.2.9.3.1.1.2.1'
&TRMSERVA    = '1.3.6.1.4.1.9.2.9.3.1.1.0.1'
&TRMSERVB    = '1.3.6.1.4.1.9.2.9.3.1.1.5.1'

&CISCONNELAP = '1.3.6.1.4.1.9.2.6.1.1.5.'
&CISCONNINBT = '1.3.6.1.4.1.9.2.6.1.1.1.'
&CISCONNOUTB = '1.3.6.1.4.1.9.2.6.1.1.2.'

&TCPCONNSTAT = '1.3.6.1.2.1.6.13.1.1.'

&CONNTYPE#1 = 'unknown'
&CONNTYPE#2 = 'PAD'
&CONNTYPE#3 = 'stream'
&CONNTYPE#4 = 'RLogin'
&CONNTYPE#5 = 'TELNET'
&CONNTYPE#6 = 'TCP'
&CONNTYPE#7 = 'LAT'
&CONNTYPE#8 = 'MOP'
&CONNTYPE#9 = 'SLIP'
&CONNTYPE#10 = 'Xremote'
*
GETMSIZE NUMLINE
&IF NUMLINE LT 21  &THEN  &GOTO  -MSGLOOP
  WRITE 'SNMPALRT: TRAP MESSAGE greater than 20 lines'
  &EXIT

-MSGLOOP
  &IF &LINECOUNT GT &NUMLINE  &THEN  &GOTO  -NOMORE
  GETMLINE LINE &LINECOUNT
  PARSEL2R LINE VAR1 VAR2 VAR3 VAR4 VAR5 VAR6
  PARSEL2R VAR1 /SNM/ MSGNUM ***
  &IF .&MSGNUM LT .040    &THEN  &GOTO  -GOODTRAP
  &IF .&MSGNUM GT .029    &THEN  &GOTO  -GOODTRAP
     WRITE 'SNMPALRT: Invalid trap message encountered'
     WRITE 'Line # = ' &LINECOUNT
     WRITE 'Msg =    ' &LINE
     &EXIT
*
-GOODTRAP
  &IF .&VAR1 EQ .SNM030I  &THEN  &REQUEST  = &VAR4
  &IF .&VAR1 EQ .SNM031I  &THEN  &AGADDR   = &VAR4
  &IF .&VAR1 EQ .SNM032I  &THEN  &GTRAP    = &VAR5
  &IF .&VAR1 EQ .SNM033I  &THEN  &STRAP    = &VAR5
  &IF .&VAR1 EQ .SNM034I  &THEN  &TMESTMP  = &VAR4
  &IF .&VAR1 NE .SNM035I  &THEN  &GOTO     -NOT35
  &ENTOBJ   = &VAR5
  PARSEL2R ENTOBJ /&CISASNID/ REMAINDER
  &IF .&REMAINDER EQ .  &THEN  &GOTO  -TRYIBM
  &INETASNID = CISCO
  &GOTO  -BUMPCOUNT
-TRYIBM
  PARSEL2R ENTOBJ /&IBMASNID/ REMAINDER
  &IF .&REMAINDER NE .  &THEN  &INETASNID = IBM
  &GOTO  -BUMPCOUNT
*
-NOT35
  &IF .&VAR1 NE .SNM036I  &THEN  &GOTO     -MORE
  &MIBVAR = &MIBVAR + 1
  &MIBVNAME#&MIBVAR = &VAR4
  &GOTO   -BUMPCOUNT
*
-MORE
  &IF .&VAR1 EQ .SNM037I  &THEN  &MIBVTYPE#&MIBVAR = &VAR4
  &IF .&VAR1 EQ .SNM038I  &THEN  &MIBVALUE#&MIBVAR = &VAR4
  &IF .&VAR1 EQ .SNM039I  &THEN  &GOTO     -NOMORE
*
-BUMPCOUNT
  &LINECOUNT = &LINECOUNT + 1
  &GOTO  -MSGLOOP
*
-NOMORE
 &HOSTCOUNT = 1
 &NUMHOSTS  = 24
*
 &HOSTIP1    = 'nnn.nnn.n.nnn'
 &RESLOC1    = 'loc1'
 &RESTYPE1   = 'type'

 &HOSTIP13   = 'nnn.nnn.nnn.nnn'
 &RESLOC13   = 'loc1'
 &RESTYPE13  = 'type'

 &HOSTIP2    = 'nnn.nnn.n.nnn'
 &RESLOC2    = 'loc1'
 &RESTYPE2   = 'type'

 &HOSTIP14   = 'x'
 &RESLOC14   = 'x'
 &RESTYPE14  = 'x'

 &HOSTIP3    = 'nnn.nnn.nn.nnn'
 &RESLOC3    = 'loc2'
 &RESTYPE3   = 'type'

 &HOSTIP15   = 'nnn.nnn.nnn.nnn'
 &RESLOC15   = 'loc2'
 &RESTYPE15  = 'type'

 &HOSTIP4    = 'nnn.nnn.nn.nnn'
 &RESLOC4    = 'loc3'
 &RESTYPE4   = 'type'

 &HOSTIP16   = 'nnn.nnn.nnn.nnn'
 &RESLOC16   = 'loc3'
 &RESTYPE16  = 'type'

 &HOSTIP5    = 'nnn.nnn.nn.nnn'
 &RESLOC5    = 'loc4'
 &RESTYPE5   = 'type'

 &HOSTIP17   = 'nnn.nnn.nnn.nnn'
 &RESLOC17   = 'loc4'
 &RESTYPE17  = 'type'

 &HOSTIP6    = 'nnn.nnn.nn.nnn'
 &RESLOC6    = 'loc5'
 &RESTYPE6   = 'type'

 &HOSTIP18   = 'nnn.nnn.nnn.nnn'
 &RESLOC18   = 'loc5'
 &RESTYPE18  = 'type'

 &HOSTIP7    = 'nnn.nnn.nn.nnn'
 &RESLOC7    = 'loc6'
 &RESTYPE7   = 'type'

 &HOSTIP19   = 'nnn.nnn.nnn.nnn'
 &RESLOC19   = 'loc6'
 &RESTYPE19  = 'type'

 &HOSTIP8    = 'nnn.nnn.nn.nnn'
 &RESLOC8    = 'loc7'
 &RESTYPE8   = 'type'

 &HOSTIP20   = 'nnn.nnn.nnn.nnn'
 &RESLOC20   = 'loc7'
 &RESTYPE20  = 'type'

 &HOSTIP9    = 'nnn.nnn.nn.nnn'
 &RESLOC9    = 'loc8'
 &RESTYPE9   = 'type'

 &HOSTIP21   = 'nnn.nnn.nnn.nnn'
 &RESLOC21   = 'loc8'
 &RESTYPE21  = 'type'

 &HOSTIP10   = 'nnn.nnn.nn.nnn'
 &RESLOC10   = 'loc9'
 &RESTYPE10  = 'type'

 &HOSTIP22   = 'nnn.nnn.nnn.nnn'
 &RESLOC22   = 'loc9'
 &RESTYPE22  = 'type'

 &HOSTIP11   = 'nnn.nnn.nnn.nnn'
 &RESLOC11   = 'loc10'
 &RESTYPE11  = 'type'

 &HOSTIP23   = 'nnn.nnn.nnn.nnn'
 &RESLOC23   = 'loc10'
 &RESTYPE23  = 'type'

 &HOSTIP12   = 'nnn.nnn.nnn.nnn'
 &RESLOC12   = 'loc11'
 &RESTYPE12  = 'type'

 &HOSTIP24   = 'nnn.nnn.nnn.nnn'
 &RESLOC24   = 'loc11'
 &RESTYPE24  = 'type'

*
-CONTINUE2
 &IF &HOSTCOUNT GT  &NUMHOSTS          &THEN  &GOTO  -UNKNOWNHOST
 &IF .&AGADDR   EQ .&HOSTIP&HOSTCOUNT  &THEN  &GOTO  -KNOWNHOST
 &HOSTCOUNT = &HOSTCOUNT + 1
 &GOTO -CONTINUE2
*
-UNKNOWNHOST
 &IF .&AGADDR EQ .    &THEN  &AGADDR = ',.,.NoIPaddr'
 PARSEL2R AGADDR /./ . /./ SUBDMADDR
 &RSRCLOC = &SUBDMADDR
 &RSRCTYPE = 'Unknown'
 &GOTO -SETUP
*
-KNOWNHOST
 &RSRCLOC = &RESLOC&HOSTCOUNT
 &RSRCTYPE = &RESTYPE&HOSTCOUNT
*
-SETUP
 &IF .&AGADDR EQ .&HOSTIP1  &THEN  &GOTO  -LOCALONLY
 &IF .&AGADDR EQ .&HOSTIP2  &THEN  &GOTO  -LOCALONLY
 &AAA = &CONCAT ' HIER=<loc>'
 &AAA = &CONCAT &AAA ',8232,'
 &AAA = &CONCAT &AAA '<loc>'
 &AAA = &CONCAT &AAA ',MAU,'
 &AAA = &CONCAT &AAA 'FDDI'
 &AAA = &CONCAT &AAA ',NODE,'
 &AAA = &CONCAT &AAA &RSRCLOC
 &AAA = &CONCAT &AAA ','
 &AAA = &CONCAT &AAA &RSRCTYPE
 &GOTO  -SETCHECK
*
-LOCALONLY
 &AAA = &CONCAT ' HIER=<loc>'
 &AAA = &CONCAT &AAA ',8232,'
 &AAA = &CONCAT &AAA '<loc>'
 &AAA = &CONCAT &AAA ',MAU,'
 &AAA = &CONCAT &AAA &RSRCLOC
 &AAA = &CONCAT &AAA ','
 &AAA = &CONCAT &AAA &RSRCTYPE
*
-SETCHECK
 &CHECK = &CONCAT 'AGENT ADDR:' &AGADDR
 &CHECK = &CONCAT &CHECK ', Specific Trap Code:'
 &CHECK = &CONCAT &CHECK &STRAP
 &CHECK = &CONCAT &CHECK ', OBJID:'
 &CHECK = &CONCAT &CHECK &ENTOBJ
*&CHECK = &CONCAT &CHECK &REQUEST
*&AGENT = &CONCAT 'AG' &REQUEST
 &CMDDATA = &CONCAT 'G TYPE=NTFY ALID=' &REQUEST
*
 &IF .&GTRAP EQ .0   &THEN   &GOTO  -COLDSTA
 &IF .&GTRAP EQ .1   &THEN   &GOTO  -WARMSTA
 &IF .&GTRAP EQ .2   &THEN   &GOTO  -LINKDOW
 &IF .&GTRAP EQ .3   &THEN   &GOTO  -LINKUP
 &IF .&GTRAP EQ .4   &THEN   &GOTO  -AUTHFAI
 &IF .&GTRAP EQ .5   &THEN   &GOTO  -EGPLOSS
 &IF .&GTRAP EQ .6   &THEN   &GOTO  -ENTERPR
*
 WRITE 'Wrong GTRAP:' &GTRAP
 &EXIT
*
*
-COLDSTA
 &BB3 = ' DESC=B000 PRID=TCPIPSNMP PC=7300 ACTS=2000'
 &CHECK = &CONCAT 'SNMPTrap:ColdStart,' &CHECK

 &CMDDATA = &CONCAT &CMDDATA &BB3
 &CMDDATA = &CONCAT &CMDDATA &AAA
*
 &GOTO  -GENALERT
*
-WARMSTA
 &BB3 = ' DESC=B000 PRID=TCPIPSNMP PC=7300 ACTS=2000'
 &CHECK = &CONCAT 'SNMPTrap:WarmStart, ' &CHECK

 &CMDDATA = &CONCAT &CMDDATA &BB3
 &CMDDATA = &CONCAT &CMDDATA &AAA
*
 &GOTO  -GENALERT
*
-LINKDOW
 &BB3 = ' DESC=3300 PRID=TCPIPSNMP PC=2000'
 &BB4 = ' FAIL=2300,3200,3300,3400,3500;0000,1000'
 &TMP1 = &CONCAT 'SNMPTrap:LinkDn VARVALUE=' &IFINDEX
 &CHECK = &CONCAT ', ' &CHECK
 &CHECK = &CONCAT &TMP1 &CHECK

 &CMDDATA = &CONCAT &CMDDATA &BB3
 &CMDDATA = &CONCAT &CMDDATA &BB4
 &CMDDATA = &CONCAT &CMDDATA &AAA
*
 &GOTO  -GENALERT
*
-LINKUP
 &BB3 = ' DESC=B000 PRID=TCPIPSNMP PC=2000 ACTS=2000'
 &TMP1 = &CONCAT 'SNMPTrap:LinkUp VARVALUE=' &IFINDEX
 &CHECK = &CONCAT ', ' &CHECK
 &CHECK = &CONCAT &TMP1  &CHECK

 &CMDDATA = &CONCAT &CMDDATA &BB3
 &CMDDATA = &CONCAT &CMDDATA &AAA
*
 &GOTO  -GENALERT
*
-AUTHFAI
 &BB3 = ' DESC=C000 PRID=TCPIPSNMP PC=FE00 ACTS=2000'
 &CHECK = &CONCAT 'SNMPTrap:Authentication Fails, ' &CHECK

 &CMDDATA = &CONCAT &CMDDATA &BB3
 &CMDDATA = &CONCAT &CMDDATA &AAA
*
 &GOTO  -GENALERT
*
-EGPLOSS
 &BB3 = ' DESC=3300 PRID=TCPIPSNMP PC=2000'
 &BB4 = ' FAIL=2300,3200,3300,3400,3500;0000,1000'
 &CHECK = &CONCAT 'SNMPTrap:EGPneighborLoss,' &CHECK

 &CMDDATA = &CONCAT &CMDDATA &BB3
 &CMDDATA = &CONCAT &CMDDATA &BB4
 &CMDDATA = &CONCAT &CMDDATA &AAA
*
 &GOTO  -GENALERT
*
-ENTERPR
 &BB3 = ' DESC=B000 PRID=TCPIPSNMP PC=FE00 ACTS=2000'

 &CMDDATA = &CONCAT &CMDDATA &BB3
 &CMDDATA = &CONCAT &CMDDATA &AAA

 &IF .&INETASNID = .CISCO   &THEN  &GOTO  -CISCOSPEC

 &CHECK = &CONCAT 'SNMPTrap:Enterprise Specific, ' &CHECK
 &GOTO  -GENALERT
*
*
-CISCOSPEC
 &IF .&STRAP EQ .1  &THEN   &GOTO  -CISTCPCLOSE
 &IF .&STRAP NE .0  &THEN   &GOTO  -CISUNKNOWN
* CiscoReload
 &TEMP = &CONCAT 'SNMPTrap:Cisco Specific, reload reported by' &AGADDR
 &TEMP = &CONCAT &TEMP ', '
 &CHECK = &CONCAT &TEMP &CHECK
 &GOTO  -GENALERT
*
-CISUNKNOWN
 &TEMP = 'SNMPTrap:Cisco Specific, UNKNOWN specific trap:'&STRAP
 &TEMP = &CONCAT &TEMP ', '
 &CHECK = &CONCAT &TEMP &CHECK
 &GOTO  -GENALERT
*
-CISTCPCLOSE
 &IF .&MIBVNAME#1 EQ .&AGSINTCLIEN  &THEN  &SERVICE = 'AgsIntClien'
 &IF .&MIBVNAME#1 EQ .&TRMSERVA     &THEN  &SERVICE = 'TrmServ.0.1'
 &IF .&MIBVNAME#1 EQ .&TRMSERVB     &THEN  &SERVICE = 'TrmServ.5.1'
 &IF .&MIBVNAME#1 NE .&AGSINTCLIEN  &THEN +
     PARSEL2R MIBVNAME#1 /&CISASNID/ SERVICE

 PARSEL2R MIBVNAME#2 /&TCPCONNSTAT/ FROMTO
 &IF &RETCODE NE 0  &THEN  &WRITE . PARSEL2R MIBVNAME#2 ... = &RETCODE
 PARSEL2R FROMTO /./ /./ /./ /./ /./ FROMADDR
 &IF &RETCODE NE 0  &THEN  &WRITE . PARSEL2R FROMTO ... = &RETCODE

 &FROMTOLEN = &LENGTH &FROMTO
 &FROMLEN = &LENGTH &FROMADDR
 &TOLEN = &FROMTOLEN - &FROMLEN
 &TOLEN = &TOLEN - 1
 &TOADDR = &SUBSTR &FROMTO 1 &TOLEN

 &CHECK = 'SNMPTrap:Cisco, tcpConnectionClose reported by:'
 &CHECK = &CONCAT &CHECK &AGADDR
 &CHECK = &CONCAT &CHECK ' for:'
 &CHECK = &CONCAT &CHECK &SERVICE
 &CHECK = &CONCAT &CHECK ' from:'
 &CHECK = &CONCAT &CHECK &FROMADDR
 &CHECK = &CONCAT &CHECK ' to:'
 &CHECK = &CONCAT &CHECK &TOADDR
*
* this event does not warrant an ALERT, so simply write it to the log
*
 &WRITE ...
 &WRITE ...
 &WRITE ... &CHECK
 &WRITE ...
 &WRITE ...
 &EXIT
*
* issue GENALERT command; make sure that TEXT <= 140 chars
*
-GENALERT
 &TEXTLEN = &LENGTH &CHECK
 &IF &TEXTLEN GT 140  &THEN  &CHECK = &SUBSTR &CHECK 1 140
*
 GENALERT &CMDDATA +
    TEXT='&CHECK'
&EXIT

